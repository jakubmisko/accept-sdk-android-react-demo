apply plugin: 'me.tatarka.retrolambda' // must be before application
apply plugin: 'com.android.application'


repositories {
    maven { url "https://jitpack.io" }
    flatDir {
        dirs 'libs'
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.1'
    defaultConfig {
        applicationId "de.wirecard.accept.sample"
        minSdkVersion 11
        targetSdkVersion 21
        versionCode 5
        versionName "1.4"
    }
    packagingOptions {
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/services/com.fasterxml.jackson.core.JsonFactory'

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        all {
            def Properties appProps = new Properties()
            try {
                appProps.load(new FileInputStream(file("../../AcceptSDKAndroidDemo.properties")))
            } catch (IOException e) {
                appProps.put("clientID", System.getenv('BACKEND_DEFAULT_UID'))
                appProps.put("clientSecret", System.getenv('BACKEND_DEFAULT_SECRET'))
                appProps.put("apiPath", System.getenv('BACKEND_DEFAULT_URL'))
            }
            buildConfigField "String", "clientID", appProps.getProperty("clientID", "\"\"")
            buildConfigField "String", "clientSecret", appProps.getProperty("clientSecret", "\"\"")
            buildConfigField "String", "apiPath", appProps.getProperty("apiPath", "\"\"")
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(output.outputFile.parent, output.outputFile.name.replace(".apk", "-" + defaultConfig.versionName + ".apk"))
        }
    }
    productFlavors {
        flavor_bbpos {
            applicationId 'de.wirecard.accept.sample.bbpos'
        }
        flavor_thyron {
            applicationId 'de.wirecard.accept.sample.thyron'
        }
    }

    android {
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
    }
}

dependencies {
    //    compile(name: 'accept-sdk-android-acceptsdksource-release', ext: 'aar')
    //    flavor_thyronCompile(name: 'accept-sdk-android-thyronextension-release', ext: 'aar')
    //    flavor_bbposCompile(name: 'accept-sdk-android-bbposextension-release', ext: 'aar')
    flavor_thyronCompile 'com.github.mposSVK:accept-android-extension-spire:1.5.3'
    flavor_bbposCompile 'com.github.mposSVK:accept-android-extension-bbpos:1.5.1'
    //compile 'com.jakewharton.rxbinding:rxbinding:0.3.0'
//    compile 'io.reactivex:rxjava-async-util:0.21.0'
    //compile 'com.squareup.retrofit2:retrofit:2.0.1'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'org.apache.httpcomponents:httpclient-android:4.3.5'
    compile 'org.apache.httpcomponents:httpmime:4.3.5'
    compile 'com.github.mposSVK:acceptSDK-Android:1.5.4'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'io.reactivex:rxjava:1.1.0'
    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'com.jakewharton:butterknife:8.1.0'
    compile 'com.android.support:design:23.4.0'
    compile 'info.android15.nucleus:nucleus:2.0.6'
    compile 'com.android.support:support-v4:23.4.0'
    compile 'com.android.support:recyclerview-v7:23.4.0'
}
