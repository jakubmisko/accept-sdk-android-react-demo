apply plugin: 'me.tatarka.retrolambda' // must be before application
apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

repositories {
    maven { url "https://jitpack.io" }
    maven { url "https://clojars.org/repo/" }
    flatDir {
        dirs 'libs'
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion '23.0.3'
    defaultConfig {
        applicationId "de.wirecard.accept.sample"
        minSdkVersion 12
        targetSdkVersion 25
        versionCode 5
        versionName "1.4"
    }
    packagingOptions {
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/services/com.fasterxml.jackson.core.JsonFactory'

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        all {
            def Properties appProps = new Properties()
            try {
                appProps.load(new FileInputStream(file("../../AcceptSDKAndroidDemo.properties")))
            } catch (IOException e) {
                appProps.put("clientID", System.getenv('BACKEND_DEFAULT_UID'))
                appProps.put("clientSecret", System.getenv('BACKEND_DEFAULT_SECRET'))
                appProps.put("apiPath", System.getenv('BACKEND_DEFAULT_URL'))
            }
            buildConfigField "String", "clientID", appProps.getProperty("clientID", "\"\"")
            buildConfigField "String", "clientSecret", appProps.getProperty("clientSecret", "\"\"")
            buildConfigField "String", "apiPath", appProps.getProperty("apiPath", "\"\"")
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(output.outputFile.parent, output.outputFile.name.replace(".apk", "-" + defaultConfig.versionName + ".apk"))
        }
    }
    productFlavors {
        flavor_bbpos {
            applicationId 'de.wirecard.accept.sample.bbpos'
        }
        flavor_thyron {
            applicationId 'de.wirecard.accept.sample.thyron'
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {
    compile(name: 'accept-sdk-android-acceptsdksource-release', ext: 'aar')
    flavor_thyronCompile(name: 'accept-sdk-android-thyronextension-release', ext: 'aar')
    flavor_bbposCompile(name: 'accept-sdk-android-bbposextension-release', ext: 'aar')
// https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-databind
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.5'

    compile 'com.android.support:appcompat-v7:25.1.0'
//    compile 'com.github.mposSVK:acceptSDK-Android:1.5.4'
//    flavor_thyronCompile 'com.github.mposSVK:accept-android-extension-spire:1.5.3'
//    flavor_bbposCompile 'com.github.mposSVK:accept-android-extension-bbpos:1.5.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'io.reactivex:rxjava:1.2.4'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava-async-util:0.21.0'
    compile 'frankiesardo:icepick:3.2.0'
    provided 'frankiesardo:icepick-processor:3.2.0'
//    compile 'com.trello:rxlifecycle-components:1.0'
    compile 'com.jakewharton:butterknife:8.1.0'
    compile 'com.android.support:design:25.1.0'
    compile 'info.android15.nucleus:nucleus:2.0.6'
    compile 'com.android.support:support-v4:25.1.0'
    compile 'com.android.support:recyclerview-v7:25.1.0'
    apt 'com.jakewharton:butterknife-compiler:8.1.0'
    testCompile "org.robolectric:robolectric:3.1"
}
