apply plugin: 'me.tatarka.retrolambda' // must be before application
apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

repositories {
    maven { url "https://jitpack.io" }
    maven { url "https://clojars.org/repo/" }
    flatDir {
        dirs 'libs'
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    defaultConfig {
        applicationId "com.wirecard.accept.sample"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 5
        versionName "1.0"
    }
    packagingOptions {
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/services/com.fasterxml.jackson.core.JsonFactory'

    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            debuggable true
            zipAlignEnabled true
            jniDebuggable true
        }
        all {
            def Properties appProps = new Properties()
            try {
                appProps.load(new FileInputStream(file("../../AcceptSDKAndroidDemo.properties")))
            } catch (IOException e) {
                appProps.put("clientID", System.getenv('BACKEND_DEFAULT_UID'))
                appProps.put("clientSecret", System.getenv('BACKEND_DEFAULT_SECRET'))
                appProps.put("apiPath", System.getenv('BACKEND_DEFAULT_URL'))
            }
            buildConfigField "String", "clientID", appProps.getProperty("clientID", "\"\"")
            buildConfigField "String", "clientSecret", appProps.getProperty("clientSecret", "\"\"")
            buildConfigField "String", "apiPath", appProps.getProperty("apiPath", "\"\"")
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(output.outputFile.parent, output.outputFile.name.replace(".apk", "-" + defaultConfig.versionName + ".apk"))
        }
    }
    productFlavors {
        flavor_bbpos {
            applicationId 'com.wirecard.accept.bbpos'
        }
        flavor_thyron {
            applicationId 'com.wirecard.accept.thyron'
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests.all {
            systemProperty 'robolectric.logging.enabled', true
            unitTests.returnDefaultValues = true
        }
        useLibrary 'org.apache.http.legacy'
    }


}

dependencies {
    //sdk
    compile(name: 'accept-sdk-android-acceptsdksource-release', ext: 'aar')
    flavor_thyronCompile(name: 'accept-sdk-android-thyronextension-release', ext: 'aar')
    flavor_bbposCompile(name: 'accept-sdk-android-bbposextension-release', ext: 'aar')
    //sdk dependencies
    compile group: 'org.apache.httpcomponents', name: 'httpclient-android', version: '4.3.5'
    compile group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.3.5'
    //sdk obfuscation
    compile project(':proguard-rules')
    //support
    compile project(':uicomponents')
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.8.5'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'io.reactivex:rxjava:1.2.6'
    compile 'com.artemzin.rxjava:proguard-rules:1.2.6.0'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava-async-util:0.21.0'
    compile 'frankiesardo:icepick:3.2.0'
    compile 'com.jakewharton:butterknife:8.1.0'
    compile 'info.android15.nucleus:nucleus:4.0.0'
    compile 'info.android15.nucleus:nucleus-support-v7:4.0.0'
    testCompile 'junit:junit:4.12'
    apt 'com.jakewharton:butterknife-compiler:8.1.0'
    provided 'frankiesardo:icepick-processor:3.2.0'
}
